const dir_right = 0
const dir_left = 1

const anim_idle = 0
const anim_walk = 2

property dir
property anim
property speed_x
property speed_y
property jumping

sub init
    play animation, "player_idle_right.mge"

    dir = dir_right
	anim = anim_idle
    type = 1
    x = 100*8
	y = 100*8
	speed_x = 0
	speed_y = 0
	jumping = 0
endsub

sub update
    call update_keys
	call update_anim
	call update_move
endsub

sub update_keys
    ' inc speed to the left
    if keydown "right" then
		if speed_x < 16 then
		     speed_x = speed_x + 1
		endif
	else
		if speed_x > 0 then
			speed_x = speed_x - 1
		endif
	endif

    ' inc speed to the right
    if keydown "left" then
		if speed_x > -16 then
		     speed_x = speed_x - 1
		endif
	else
		if speed_x < 0 then
			speed_x = speed_x + 1
		endif
	endif

    ' jump
	if jumping = 0 then
	    if keydown "up" then
		    speed_y = -32
			jumping = 1
		endif
	endif

    ' slow down speed y
	if jumping > 0 then
        jumping = jumping + 1
		if jumping = 33 then
		    jumping = 0
			speed_y = 0
		else
	        speed_y = speed_y + 2
	    endif
	endif
endsub

sub update_anim
    ' change anim to idle
    if speed_x = 0 then
	    if anim <> anim_idle then
		    if dir = dir_right then
				play animation, "player_idle_right.mge"
			else
				play animation, "player_idle_left.mge"
			endif
		endif
		anim = anim_idle
	    return
	endif

    ' change anim to walk right
    if speed_x > 0 then
	    if anim <> anim_walk then
		    play animation, "player_walk_right.mge"
		endif
		if dir = dir_left then
		    play animation, "player_walk_right.mge"
		endif
		anim = anim_walk
		dir = dir_right
		return
	endif

    ' change anim to walk left
    if speed_x < 0 then
	    if anim <> anim_walk then
		    play animation, "player_walk_left.mge"
		endif
		if dir = dir_right then
		    play animation, "player_walk_left.mge"
		endif
		anim = anim_walk
		dir = dir_left
		return
	endif
endsub

sub update_move
    x = x + speed_x
	y = y + speed_y
endsub
