global nbullets

const state_idle = 0
const state_walking = 1
const state_dead = 2

property state
property next_state

sub init
    play animation, "player_idle.mge"
    type = 1
    width = 6
    height = 7
    x = 100*8
    y = 100*8

    state = state_idle
endsub

sub update
    if state = state_dead then
       return
    endif

    next_state = state_idle

    ' move player
    if keydown "left" then
        if x > 0 then
            x = x - 8
            next_state = state_walking
        endif
    endif
    if keydown "right" then
        if x < 239*8 then
            x = x + 8
            next_state = state_walking
        endif
    endif
    if keydown "up" then
        if y > 0 then
            y = y - 8
            next_state = state_walking
        endif
    endif
    if keydown "down" then
        if y < 175*8 then
            y = y + 8
            next_state = state_walking
        endif
    endif

    ' change current state
    if next_state = state_walking then
       if state = state_idle then
          state = state_walking
          play animation, "player_right.mge"
       endif
    else
       if state = state_walking then
          state = state_idle
          play animation, "player_idle.mge"
       endif
    endif

    ' fire bullet
    if keypressed "space" then
        if nbullets < 2 then
            new sprite, "bullet.mge", x, y
        endif
    endif

    ' player missile collision
    if collide 2 then
       call dead
       return
    endif

    ' player tile collision
    if tile 4, 8 = 1 then
        call dead
        return
    endif
    if tile 12, 8 = 1 then
        call dead
        return
    endif
    if tile 8, 4 = 1 then
        call dead
        return
    endif
    if tile 8, 12 = 1 then
        call dead
        return
    endif
endsub

sub dead
    play animation, "player_dead.mge"
    state = state_dead
endsub