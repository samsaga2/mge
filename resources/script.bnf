prog = (<nl-comment>|<ws>)? sub (<nl-comment> sub)* (<nl-comment>|<ws>)?

nl = #'[\n\r]+'
comment = #'\s*\'[^\n\r]*[\n\r]+'
nl-comment = (comment|nl)
ws = #'\s+'
id = #'[a-zA-Z_][a-zA-Z_0-9]*'
num = #'-?[0-9]+'
str = <'"'> #'[^"]*' <'"'>
comma = <ws>? <','> <ws>? 

sub = <'sub'> <ws> id <nl-comment> op* <'endsub'>
<op> = <ws>? (load|sprite-ops|if-ops|new-ops|return|assign|call|music-stop|sfx-play|scroll) <nl-comment>+

<arg> = (id|num)

<new-ops> = <'new'> <ws> (new-sprite)
new-sprite = <'sprite'> <comma> str <ws>? [<','> <ws>? arg]*

<sprite-ops> = (sprite-pos|sprite-move|sprite-type|sprite-width|sprite-height|sprite-delete)
sprite-pos = <'pos'> <ws> arg <comma> arg
sprite-move = <'move'> <ws> arg <comma> arg
sprite-type = <'type'> <ws> arg
sprite-width = <'width'> <ws> arg
sprite-height = <'height'> <ws> arg
sprite-delete = <'delete'>

if-ops = (<'if'> <ws> (if-keydown|if-keypressed|if-collide|if-cmp) <ws> <'then'> <nl-comment> <ws>? then)
then = op* <ws>? (<'endif'>|else)
else = <'else'> op* <ws>? <'endif'>
if-keydown = <'keydown'> <ws>? str
if-keypressed = <'keypressed'> <ws>? str
if-cmp = arg <ws>? ('='|'<>'|'<'|'>'|'>='|'<=') <ws>? arg
if-collide = <'collide'> <ws>? arg

return = <'return'>
call = <'call'> <ws> id <ws>? [<','> <ws>? arg]*
load = <'load'> <ws> ('image'|'title'|'animation'|'music'|'sfx'|'tilemap') <comma> str
music-stop = <'music'> <ws> <'stop'> 
sfx-play = <'sfx'> <ws> <'play'> <ws> arg

<assign> = (assign-val|assign-add|assign-sub)
assign-val = id <ws>? <'='> <ws>? arg
assign-add = id <ws>? <'='> <ws>? arg <ws>? <'+'> <ws>? arg
assign-sub = id <ws>? <'='> <ws>? arg <ws>? <'-'> <ws>? arg

<scroll> = <'scroll'> <ws> (scroll-right|scroll-left)
scroll-right = <'right'>
scroll-left = <'left'>