prog = (<nl>|<ws>)? sub (<nl> sub)* (<nl>|<ws>)?
<ops> = (<nl> sub)*

nl = #'[\n\r]+'
ws = #'\s+'
id = #'[a-zA-Z_][a-zA-Z_0-9]*'
num = #'-?[0-9]+'
str = <'"'> #'[^"]*' <'"'>

sub = <'sub'> <ws> id <nl> op* <'endsub'>
<op> = <ws>? (sprite-ops|if-ops|new-ops|title|return|assign|call) <nl>+

<arg> = (id|num)

<new-ops> = <'new'> <ws> (new-sprite)
new-sprite = <'sprite'> <ws> str <ws>? [<','> <ws>? arg]*

<sprite-ops> = (sprite-pos|sprite-image|sprite-move|sprite-type|sprite-width|sprite-height|sprite-delete)
sprite-pos = <'pos'> <ws> arg <ws>? <','> <ws>? arg
sprite-image = <'image'> <ws> str
sprite-move = <'move'> <ws> arg <ws>? <','> <ws>? arg
sprite-type = <'type'> <ws> arg
sprite-width = <'width'> <ws> arg
sprite-height = <'height'> <ws> arg
sprite-delete = <'delete'>

if-ops = (<'if'> <ws> (if-keydown|if-keypressed|if-collide|if-cmp) <ws> <'then'> <nl> <ws>? then)
then = op* <ws>? (<'endif'>|else)
else = <'else'> op* <ws>? <'endif'>
if-keydown = <'keydown'> <ws>? str
if-keypressed = <'keypressed'> <ws>? str
if-cmp = arg <ws>? ('='|'<>'|'<'|'>'|'>='|'<=') <ws>? arg
if-collide = <'collide'> <ws>? arg

title = <'title'> <ws> str
return = <'return'>
call = <'call'> <ws> id <ws>? [<','> <ws>? arg]*

<assign> = (assign-val|assign-add|assign-sub)
assign-val = id <ws>? <'='> <ws>? arg
assign-add = id <ws>? <'='> <ws>? arg <ws>? <'+'> <ws>? arg
assign-sub = id <ws>? <'='> <ws>? arg <ws>? <'-'> <ws>? arg