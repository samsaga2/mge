prog = (<nl-comment>|<ws>)? op (<nl-comment> op)* (<nl-comment>|<ws>)?

nl = #'[\n\r]+'
comment = #'\s*\'[^\n\r]*[\n\r]+'
nl-comment = (comment|nl)
ws = #'\s+'
id = #'[a-zA-Z_][a-zA-Z_0-9]*'
num = #'-?[0-9]+'
str = <'"'> #'[^"]*' <'"'>
comma = <ws>? <','> <ws>? 

<arg> = (id|num)

<op> = <ws>? (anim-load|load|next-frame|new-ops|sprite-delete|music-stop|sfx-play|scroll|set-tile|print-str|assign)

anim-load = <'load'> <ws> 'animation' <comma> str
load = <'load'> <ws> ('image'|'title'|'music'|'sfx'|'screen') <comma> str
next-frame = <'next'> <ws> <'frame'>
music-stop = <'music'> <ws> <'stop'> 
sfx-play = <'sfx'> <ws> <'play'> <ws> arg

<new-ops> = <'new'> <ws> (new-sprite)
new-sprite = <'sprite'> <comma> str <ws>? [<','> <ws>? arg]*

sprite-delete = <'delete'>

<assign> = (assign-val|assign-add|assign-sub)
assign-val = id <ws>? <'='> <ws>? arg
assign-add = id <ws>? <'='> <ws>? arg <ws>? <'+'> <ws>? arg
assign-sub = id <ws>? <'='> <ws>? arg <ws>? <'-'> <ws>? arg

<scroll> = <'scroll'> <ws> (scroll-right|scroll-left)
scroll-right = <'right'>
scroll-left = <'left'>
set-tile = <'set'> <ws> <'tile'> <ws> arg <comma> arg <comma> arg
print-str = <'print'> <ws> arg <comma> arg <comma> str
