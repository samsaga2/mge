prog = (<nl-comment>|<ws>)? op (<nl-comment> op)* (<nl-comment>|<ws>)?

nl = #'[\n\r]+'
comment = #'\s*\'[^\n\r]*[\n\r]+'
nl-comment = (comment|nl)
ws = #'\s+'
id = #'[a-zA-Z_][a-zA-Z_0-9]*'
num = #'-?[0-9]+'
str = <'"'> #'[^"]*' <'"'>
comma = <ws>? <','> <ws>? 

<arg> = add-sub
<add-sub> = mul-div | arg-add | arg-sub
arg-add = add-sub <ws>? <'+'> <ws>? mul-div
arg-sub = add-sub <ws>? <'-'> <ws>? mul-div
<mul-div> = term | arg-mul | arg-div
arg-mul = mul-div <ws>? <'*'> <ws>? term
arg-div = mul-div <ws>? <'/'> <ws>? term
<term> = id | num | <'('> <ws>? add-sub <ws>? <')'>

<op> = <ws>? (anim-play|play-str|play-num|load|next-frame|new-ops|sprite-delete|music-stop|sfx-play|scroll|set-tile|print|assign)

anim-play = <'play'> <ws> <'animation'> <ws>? <comma> str
load = <'load'> <ws> ('sprite'|'title'|'sfx'|'screen') <ws>? <comma> str
play-str = <'play'> <ws> ('music') <ws>? <comma> str
play-num = <'play'> <ws> ('sfx') <ws>? <comma> arg
next-frame = <'next'> <ws> <'frame'>
music-stop = <'stop'> <ws> <'music'> 
sfx-play = <'sfx'> <ws> <'play'> <ws> arg

<new-ops> = <'new'> <ws> (new-sprite)
new-sprite = <'sprite'> <comma> str <ws>? [<','> <ws>? arg]*

sprite-delete = <'delete'>

assign = id <ws>? <'='> <ws>? arg

<scroll> = <'scroll'> <ws> (scroll-right|scroll-left)
scroll-right = <'right'>
scroll-left = <'left'>
set-tile = <'set'> <ws> <'tile'> <ws> arg <comma> arg <comma> arg

<print> = (print-str|print-num)
print-str = <'print'> <ws> arg <comma> arg <comma> str
print-num = <'print'> <ws> arg <comma> arg <comma> arg
