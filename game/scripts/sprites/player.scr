global nbullets

property state ' current state (0-idle, 1-walking)
property next_state
property is_dead

sub init
    load animation, "player_idle.scr"
    type 1
    width 6
    height 7
    pos 800, 800

    state = 0
    is_dead = 0
endsub

sub update
    if is_dead = 1 then
       return
    endif

    next_state = 0

    ' move player
    if keydown "left" then
        if x > 0 then
            move -8, 0
            next_state = 1
        endif
    endif
    if keydown "right" then
        if x < 1912 then
            move 8, 0
            next_state = 1
        endif
    endif
    if keydown "up" then
        if y > 0 then
            move 0, -8
            next_state = 1
        endif
    endif
    if keydown "down" then
        if y < 1400 then
            move 0, 8
            next_state = 1
        endif
    endif

    ' change current state
    if next_state = 1 then
       if state = 0 then
          state = 1
          load animation, "player_right.scr"
       endif
    else
       if state = 1 then
          state = 0
          load animation, "player_idle.scr"
       endif
    endif

    ' fire bullet
    if keypressed "space" then
        if nbullets < 2 then
            new sprite, "bullet.scr", x, y
        endif
    endif

    ' player missile collision
    if collide 2 then
       call dead
       return
    endif

    ' player tile collision
    if tile 4, 8 = 1 then
        call dead
        return
    endif
    if tile 12, 8 = 1 then
        call dead
        return
    endif
    if tile 8, 4 = 1 then
        call dead
        return
    endif
    if tile 8, 12 = 1 then
        call dead
        return
    endif
endsub

sub dead
    load animation, "player_dead.scr"
    is_dead = 1
endsub